import "shapes/rect.aspt";
import "shapes/triangle.aspt";
import "shapes/point.aspt";

house $width $wallHeight $roofHeight = {

	$wall = rect $width $wallHeight of "X";
	$roof = triangle up $width $roofHeight of "^";

	$elements = $wall, $roof;

	$this wall = $wall;
	wall of $this = $wall;

	$this roof = $roof;
	roof of $this = $roof;

	place $this left $x bottom $y = {
		$placedWall = place $wall left $x bottom $y;
		$placedRoof = place $roof left $x bottom ($y - $wallHeight + 1);

		show $this = par(
			show $placedWall,
			show $placedRoof
		);
	};

  place $this right $x bottom $y = {
    $placedWall = place $wall right $x bottom $y;
    $placedRoof = place $roof right $x bottom ($y - $wallHeight + 1);

    show $this = par(
      show $placedWall,
      show $placedRoof
    );
  };


	show $this left $x bottom $y = show (place $this left $x bottom $y);
	show $this right $x bottom $y = show (place $this right $x bottom $y);
};


show house $width $wallHeight $roofHeight left $x bottom $y = show (house $width $wallHeight $roofHeight) left $x bottom $y;
show house $width $wallHeight $roofHeight right $x bottom $y = show (house $width $wallHeight $roofHeight) right $x bottom $y;


test house = par(
	wait 2000,
	move (show house 16 5 6 left 5 bottom 10) by 10 10
);